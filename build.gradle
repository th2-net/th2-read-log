plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.8.22'
    id 'org.jetbrains.kotlin.kapt' version '1.8.22'
    id 'com.exactpro.th2.gradle.component' version '0.0.6'
}

group 'com.exactpro.th2'
version release_version

kotlin {
    jvmToolchain(11)
}

repositories {
    mavenCentral()
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }
    mavenLocal()

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

dependencies {
    implementation "com.exactpro.th2:common:5.11.0-dev"
    implementation "com.exactpro.th2:common-utils:2.2.3-dev"
    implementation "com.exactpro.th2:read-file-common-core:3.3.0-migrate-to-th2-gradle-plugin-+"

    implementation ("com.opencsv:opencsv:5.9") {
        because("we need to write a correct CSV in case of free pattern is used")
    }

    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
    implementation "io.github.microutils:kotlin-logging:3.0.5"

    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"

    testImplementation "org.junit.jupiter:junit-jupiter:5.10.2"
    testImplementation "org.mockito:mockito-core:5.12.0"
}

test {
    useJUnitPlatform {
        excludeTags('integration-test')
    }
    testLogging.showStandardStreams = true
}

tasks.register('integrationTest', Test) {
    group = 'verification'
    useJUnitPlatform {
        includeTags('integration-test')
    }
}

application {
    mainClassName = "com.exactpro.th2.readlog.Main"
}